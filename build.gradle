plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.0'
}

group = 'tf.tuff'
version = '1.2.0'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    maven { url = 'https://repo.viaversion.com/everything/' }
    maven { url = 'https://repo.codemc.io/repository/maven-releases/' }
    maven { url = 'https://repo.codemc.io/repository/maven-snapshots/' }
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://repo.dmulloy2.net/repository/public/' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.18.2-R0.1-SNAPSHOT'
    
    implementation 'com.github.retrooper:packetevents-spigot:2.11.1'
    
    compileOnly 'com.viaversion:viabackwards:5.3.2'
    compileOnly 'com.viaversion:viaversion:5.4.1'
    
    compileOnly 'it.unimi.dsi:fastutil:8.5.16'
    
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
    
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
}

processResources {
    filesMatching('plugin.yml') {
        expand(
            'version': project.version,
            'name': project.name
        )
    }
}

shadowJar {
    archiveClassifier.set('')
    archiveFileName.set('TuffX.jar')
    
    relocate 'com.github.retrooper.packetevents', 'tf.tuff.packetevents'
    relocate 'io.github.retrooper.packetevents', 'tf.tuff.packetevents'
    relocate 'com.fasterxml.jackson', 'tf.tuff.jackson'
    relocate 'okhttp3', 'tf.tuff.okhttp3'
    relocate 'okio', 'tf.tuff.okio'
    relocate 'kotlin', 'tf.tuff.kotlin'
    
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/versions/**'
    exclude 'module-info.class'
}

tasks.named('jar') {
    enabled = false
}

tasks.named('build') {
    dependsOn shadowJar
}
